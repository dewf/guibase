module Windowing;

import Drawing;

void moduleInit();
void moduleShutdown();

void runloop();
void exitRunloop();

enum MouseButton {
   None,
   Left,
   Middle,
   Right,
   Other
}

flags Modifiers {
   Shift = 1 << 0,
   Control = 1 << 1, // command on mac
   Alt = 1 << 2,
   MacControl = 1 << 3
}

enum WindowStyle {
   Default,
   Frameless,
   PluginWindow   // for VST/AU/etc
}

options WindowOptions {
   // each of these will provide a setter and will keep track of whether it "exists" or not
   // basically a quicker way of implementing a group of optional settings without either flags or sumtypes
   int minWidth;
   int minHeight;
   int maxWidth;
   int maxHeight;
   WindowStyle style;
   // windows-only:
   size_t nativeParent; // HWND actually
}

exception DropDataBadFormat(string format); // until we have an Option<> or Result<> type for the result of getFormat()

// constants for easy checking
// might want to create a way to enumerate what's in the DropData for easy inspection ...
// but these are used by both DropData and DragData ...
string kDragFormatUTF8;
string kDragFormatFiles;

opaque DropData {
   bool hasFormat(string mimeFormat);
   Array<string> getFiles() throws DropDataBadFormat;
   string getTextUTF8() throws DropDataBadFormat;
   Buffer<byte> getFormat(string mimeFormat) throws DropDataBadFormat;
}

flags DropEffect {
   None = 0,
   Copy = 1 << 0,
   Move = 1 << 1,
   Link = 1 << 2,
   Other = 1 << 3 // ask / private / etc
}

opaque DragRenderPayload {
   void renderUTF8(string text);
   void renderFiles(Array<string> filenames);
   void renderFormat(string formatMIME, Buffer<byte> data);
}

delegate bool DragRenderFunc(string requestedFormat, DragRenderPayload payload);

opaque DragData {
   DropEffect dragExec(DropEffect canDoMask); // modal/blocking (note: currently missing *fromEvent parameter, which I think was required for GTK)

   static DragData create(Array<string> supportedFormats, DragRenderFunc renderFunc);
}

interface WindowDelegate {
   bool canClose();
   void closed();
   void destroyed();
   void mouseDown(int x, int y, MouseButton button, Modifiers modifiers);
   void mouseUp(int x, int y, MouseButton button, Modifiers modifiers);
   void mouseMove(int x, int y, Modifiers modifiers);
   void mouseEnter(int x, int y, Modifiers modifiers);
   void mouseLeave(Modifiers modifiers);
   void repaint(DrawContext context, int x, int y, int width, int height);
   void moved(int x, int y);
   void resized(int width, int height);
   void keyDown(Key key, Modifiers modifiers, KeyLocation location);

   // dnd drop
   DropEffect dropFeedback(DropData data, int x, int y, Modifiers modifiers, DropEffect suggested);
   void dropLeave();
   void dropSubmit(DropData data, int x, int y, Modifiers modifiers, DropEffect effect);
}

enum CursorStyle {
   Default,                  // IDC_ARROW, normal select
   TextSelect,               // IDC_IBEAM, text select
   BusyWait,                 // IDC_WAIT, busy
   Cross,                    // IDC_CROSS, precision select
   UpArrow,                  // IDC_UPARROW, alternate select
   ResizeTopLeftBottomRight, // IDC_SIZENWSE, diagonal resize 1
   ResizeTopRightBottomLeft, // IDC_SIZENESW, diagonal resize 2
   ResizeLeftRight,          // IDC_SIZEWE, horizontal resize
   ResizeUpDown,             // IDC_SIZENS, vertical resize
   Move,                     // IDC_SIZEALL, move
   Unavailable,              // IDC_NO, unavailable
   HandSelect,               // IDC_HAND, link select
   PointerWorking,           // IDC_APPSTARTING, working in background
   HelpSelect,               // IDC_HELP, help select
   LocationSelect,           // IDC_PIN, location select
   PersonSelect,             // IDC_PERSON, person select
   Handwriting               // MAKEINTRESOURCE(32631), pencil cursor
}

opaque Window {
   void destroy();
   void show();
   void showRelativeTo(Window other, int x, int y, int newWidth, int newHeight); // use "0" for newWidth/newHeight for no change, until we have Option<>
   void showModal(Window parent);                                                // parent = null for app-modal (otherwise centers inside of parent, and only modal for that window, I think)
   void endModal();                                                              // call to end a modal runloop that we're in
   void hide();
   void invalidate(int x, int y, int width, int height);
   void setTitle(string title);
   void focus();
   void mouseGrab();
   size_t getOSHandle();
   void enableDrops(bool enable);
   void setMenuBar(MenuBar menuBar);
   void showContextMenu(int x, int y, Menu menu); // hmm, had a "fromEvent" parameter I think for GTK
   void setCursor(CursorStyle style);

   static Window create(int width, int height, string title, WindowDelegate del, WindowOptions opts);
   static void mouseUngrab();
}

// TIMER =============================================================================

delegate void TimerFunc(double secondsSinceLast);

opaque Timer {
   static Timer create(int msTimeout, TimerFunc func);
}

// MENU ==============================================================================

opaque Icon {
   static Icon create(string filename, int sizeToWidth);
}

opaque Accelerator {
   static Accelerator create(Key key, Modifiers modifiers);
}

delegate void MenuActionFunc();

opaque MenuAction {
   static MenuAction create(string label, Icon icon, Accelerator accel, MenuActionFunc func);
}

opaque MenuItem;
opaque Menu {
   MenuItem addAction(MenuAction action);
   MenuItem addSubmenu(string label, Menu sub);
   void addSeparator();

   static Menu create();   // for menu bars and standalone popups
}

opaque MenuBar {
   void addMenu(string label, Menu menu);
   
   static MenuBar create();
}

// CLIPBOARD =========================================================================

opaque ClipData extends DropData {
   static void setClipboard(DragData dragData); // for sending
   static ClipData get();                       // really just a DropData
   static void flushClipboard();
}

// FILE DIALOGS ======================================================================

enum FileDialogMode {
   File,
   Folder
}

struct FileDialogFilterSpec {
   string description;
   Array<string> extensions; // no leading '.'
}

struct FileDialogOptions {
   Window forWindow;                     // window-modal if provided, otherwise NOT app-modal
   FileDialogMode mode;
   Array<FileDialogFilterSpec> filters;
   bool allowAll;                        // append *.*
   string defaultExt;                    // windows only at the moment (?): "sets the default extension to be added to file names"
   bool allowMultiple;                   // for open mode only
   string suggestedFilename;             // for save mode
}

struct FileDialogResult {
   bool success;
   Array<string> filenames;
}

opaque FileDialog {
   static FileDialogResult openFile(FileDialogOptions opts);
   static FileDialogResult saveFile(FileDialogOptions opts);
}

// MESSAGEBOX ========================================================================

enum MessageBoxButtons {
   Default = 0,
   AbortRetryIgnore,
   CancelTryContinue,
   Ok,                  // default
   OkCancel,
   RetryCancel,
   YesNo,
   YesNoCancel
}

enum MessageBoxIcon {
   Default = 0,
   Information,         // default
   Warning,
   Question,
   Error,
}

enum MessageBoxResult {
   Abort,
   Cancel,
   Continue,
   Ignore,
   No,
   Ok,
   Retry,
   TryAgain,
   Yes
}

struct MessageBoxParams {
   string title;           // optional?
   string message;         
   bool withHelpButton;
   MessageBoxIcon icon;
   MessageBoxButtons buttons;
}

opaque MessageBoxModal { // 'MessageBox' is a win32 function
   static MessageBoxResult show(Window forWindow, MessageBoxParams mbParams);
}

// KEY ENUM STUFF ====================================================================

enum KeyLocation {
   Default,
   Left,
   Right,
   NumPad
}

enum Key {
   Unknown,
   //
   Escape,
   Tab,
   Backspace,
   Return,
   Space,
   //
   F1,
   F2,
   F3,
   F4,
   F5,
   F6,
   F7,
   F8,
   F9,
   F10,
   F11,
   F12,
   F13,
   F14,
   F15,
   F16,
   F17,
   F18,
   F19,
   //
   _0,
   _1,
   _2,
   _3,
   _4,
   _5,
   _6,
   _7,
   _8,
   _9,
   //
   A,
   B,
   C,
   D,
   E,
   F,
   G,
   H,
   I,
   J,
   K,
   L,
   M,
   N,
   O,
   P,
   Q,
   R,
   S,
   T,
   U,
   V,
   W,
   X,
   Y,
   Z,
   // modifiers
   Control,
   Shift,
   AltOption,
   WinCommand,
   Fn,
   // home/end block
   Insert,
   Delete,
   PageUp,
   PageDown,
   Home,
   End,
   // arrow keys
   LeftArrow,
   UpArrow,
   RightArrow,
   DownArrow,
   // keypad numbers
   KP0,
   KP1,
   KP2,
   KP3,
   KP4,
   KP5,
   KP6,
   KP7,
   KP8,
   KP9,
   // keypad ops
   KPClear,
   KPEquals,
   KPDivide,
   KPMultiply,
   KPSubtract,
   KPAdd,
   KPEnter,
   KPDecimal,
   // locks
   CapsLock,
   NumLock,
   ScrollLock,
   // misc
   PrintScreen,
   Pause,  // Pause/Break button minus Ctrl
   Cancel, // Ctrl-Break
   // media
   MediaMute,
   MediaVolumeDown,
   MediaVolumeUp,
   MediaNext,
   MediaPrev,
   MediaStop,
   MediaPlayPause
}
